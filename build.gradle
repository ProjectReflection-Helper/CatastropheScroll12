plugins {
    id 'eclipse'
    id 'idea'
    //id 'net.minecraftforge.gradle' version "${forge_gradle_version}"
    id 'com.gtnewhorizons.retrofuturagradle' version '1.4.0'
//    id 'org.spongepowered.mixin' version "${mixin_gradle_version}"


}

version = mod_version
group = mod_id
base.archivesName = mod_name

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
        vendor=JvmVendorSpec.AZUL
    }
}

minecraft {
    mcVersion.set('1.12.2')
    mcpMappingChannel.set(mappings_channel)
    mcpMappingVersion.set(mappings_version)

    username.set('InDev')

    // Add any additional tweaker classes here
    //extraTweakClasses.addAll(propertyStringList('extra_tweak_classes'))

    // Add various JVM arguments here for runtime
    def args = ['-ea:' + group]
    args << '-Dmixin.hotSwap=true'
    args << '-Dmixin.checks.interfaces=true'
    args << '-Dmixin.debug.export=true'
    extraRunJvmArguments.addAll(args)
}

repositories {
    flatDir { dirs 'libs' }
    maven {
        name = "OvermindDL1 Maven"
        url = "https://gregtech.overminddl1.com/"
    }
    maven {
        name = "GTNH Maven"
        url = "https://nexus.gtnewhorizons.com/repository/public/"
    }
    maven {
        url = 'https://api.modrinth.com/maven'
        content{
            includeGroup 'maven.modrinth'
        }
    }
    maven {
        url = 'https://www.cursemaven.com'
        content{
            includeGroup 'curse.maven'
        }
    }
    maven {
        url 'https://maven.thiakil.com'
        content{
            includeGroup 'com.azanor.baubles'
        }
    }
    maven {
        url 'https://maven.blamejared.com'
        content{
            includeGroup 'mezz.jei'
            includeGroup 'net.darkhax.gamestages'
            includeGroup 'net.darkhax.bookshelf'
        }
    }
}

dependencies {
    // minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    annotationProcessor 'com.github.bsideup.jabel:jabel-javac-plugin:1.0.0'
    // Workaround for https://github.com/bsideup/jabel/issues/174
    annotationProcessor 'net.java.dev.jna:jna-platform:5.13.0'
    compileOnly ('com.github.bsideup.jabel:jabel-javac-plugin:1.0.0') {
        transitive = false
    }
    // Allow jdk.unsupported classes like sun.misc.Unsafe, workaround for JDK-8206937 and fixes crashes in tests
    patchedMinecraft 'me.eigenraven.java8unsupported:java-8-unsupported-shim:1.0.0'

    def mixin=modUtils.enableMixins("org.spongepowered:mixin:${mixin_version}","mixins.${mod_id}.refmap.json")
    api (mixin) {
        transitive = false
    }
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"
    implementation "io.github.llamalad7:mixinextras-common:${mixin_extras_version}"
    annotationProcessor "io.github.llamalad7:mixinextras-common:${mixin_extras_version}"

    implementation rfg.deobf("curse.maven:alternate-mixin-utilities-1328540:${twelvefold_booter_version}")
	
    implementation rfg.deobf("mezz.jei:jei_1.12.2:${jei_version}")
    implementation rfg.deobf("com.azanor.baubles:Baubles:1.12-1.5.2")
    implementation rfg.deobf('net.darkhax.gamestages:GameStages-1.12.2:2.0.123')
    implementation rfg.deobf('net.darkhax.bookshelf:Bookshelf-1.12.2:2.3.590')
}

//mixin {
//    add sourceSets.main, "mixins.${mod_id}.refmap.json"
//}

processResources {
    var replaceProperties = [
            'mod_id': mod_id,
            'mod_name': mod_name,
            'version': mod_version,
            'mcversion': minecraft_version,
            'mod_description': mod_description,
            'mod_author': mod_author
    ]

    inputs.properties replaceProperties

    filesMatching('mcmod.info') {
        expand replaceProperties
    }
}

jar {
    manifest {
        def currentTasks = gradle.startParameter.taskNames
        def forceLoadAsMod = (currentTasks[0] == 'build' || currentTasks[0] == 'prepareObfModsFolder' || currentTasks[0] == 'runObfClient')
        attributes([
                'Specification-Title'     : mod_name,
                'Specification-Vendor'    : mod_author,
                'Specification-Version'   : '1',
                'Implementation-Title'    : mod_name,
                'Implementation-Vendor'   : mod_author,
                'Implementation-Version'  : mod_version,
                'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                'FMLCorePlugin': mixin_loader_class,
                'FMLCorePluginContainsFMLMod': 'true',
                'ForceLoadAsMod': forceLoadAsMod,
                'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker'
        ])
    }
}
configure([tasks.compileJava]) {
    sourceCompatibility = 16 // for the IDE support
    options.release = 8

    javaCompiler = javaToolchains.compilerFor {
        languageVersion = JavaLanguageVersion.of(16)
    }
}
jar.finalizedBy('reobfJar')